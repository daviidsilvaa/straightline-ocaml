(* location.ml *)

(*
  Module Lexing: the run-time library for lexers generated by ocamllex

  type position = {      (* describes a point in a source file *)
    pos_fname : string;  (* file name *)
    pos_lnum  : int;     (* line number *)
    pos_bol   : int;     (* offset of the beginning of the line *)
    pos_cnum  : int;     (* offset of the position *)
                         (* pos_cnum - pos_bol is the character offset within the line *)
  }

  Only pos_cnum of the current position in a lexbuff is automatically
  updated by the lexing engine.

  Use the function Lexing.new_line to reflect the start of a new line.

 *)

(* type for a location in source code *)

type t = { loc_start : Lexing.position;
           loc_end : Lexing.position;
         }

(* initializing *)

let in_file name =
  let loc = { Lexing.pos_fname = name;
              Lexing.pos_lnum = 1;
              Lexing.pos_bol = -1;
              Lexing.pos_cnum = 0;
            } in
  { loc_start = loc;
    loc_end = loc;
  }

let none = in_file "_none_"

let lexbuf_ref = ref None

let init lexbuf fname =
  lexbuf_ref := Some lexbuf;
  lexbuf.Lexing.lex_curr_p <- { Lexing.pos_fname = fname;
                                Lexing.pos_lnum = 1;
                                Lexing.pos_bol = -1;
                                Lexing.pos_cnum = 0;
                              }

let curr_loc lexbuf = { loc_start = Lexing.lexeme_start_p lexbuf;
                        loc_end = Lexing.lexeme_end_p lexbuf;
                      }


(* Print a location *)

let print_pos ppf pos =
  Format.fprintf ppf "%s:%i.%i"
                 pos.Lexing.pos_fname
                 pos.Lexing.pos_lnum
                 (pos.Lexing.pos_cnum - pos.Lexing.pos_bol)

let print_loc ppf loc =
  if loc.loc_start.Lexing.pos_fname = loc.loc_end.Lexing.pos_fname then
    Format.fprintf ppf "%s:%i.%i-%i.%i"
                   loc.loc_start.Lexing.pos_fname
                   loc.loc_start.Lexing.pos_lnum
                   (loc.loc_start.Lexing.pos_cnum - loc.loc_start.Lexing.pos_bol)
                   loc.loc_end.Lexing.pos_lnum
                   (loc.loc_end.Lexing.pos_cnum - loc.loc_end.Lexing.pos_bol)
  else
    Format.fprintf ppf "%a-%a"
                   print_pos loc.loc_start
                   print_pos loc.loc_end


(* Annotating a value with a location *)

type 'a loc = t * 'a

let mkloc loc x = (loc,x)

let mknoloc x = mkloc none x

let loc (location,_) = location
